//	build.gradle
//
//	Build file for Curate Loader
//
//	01	kdoig	10-Apr-13
//  02  andrei  28-May-19   Migrated to Curate
//  03  kdoig   29-May-19   Adapted for PathOS v1.5.1
//

//  Release version
//
version = '1.5.3'

apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow'

sourceSets
{
  main {
    java { srcDirs = [] }    // no source dirs for the java compiler
    //groovy { srcDir "src/groovy" }  // compile everything in src/ with groovy
    groovy {
        srcDirs = ["src/groovy","grails-app/services","grails-app/domain"]
        exclude "**/JiraService**"
        exclude "**/curateLoginListener**"
        exclude "**/AuditService**"
    }

   }
}

sourceSets.main.resources.srcDirs += ["src/groovy/main/resources"]

//  Release version
//
apply from: '../scripts/common.gradle'

//def loaderpath = System.getenv().get("LOADER_HOME")

repositories
{
    mavenLocal()

    jcenter()

    flatDir {
        dirs "../repos"
    }
}


//  shadowjar additions
//
buildscript
{
    repositories {
        maven {
            url "https://jcenter.bintray.com"
            jcenter()  // this is for shadowjar only
        }
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
    }
}

shadowJar
{
    archiveName = "Loader-all-${version}.${extension}"

    mergeServiceFiles('META-INF/spring.*')
}

//  JAR settings
//
jar
{
    baseName = 'Loader'

    manifest
    {
        attributes 'Implementation-Title': 'Loader', 'Implementation-Version': version
    }
}

artifacts {
    archives shadowJar {
        baseName = 'Loader'
        classifier = 'all'
        with jar
    }
    archives distTar
}

distributions {
    main {
        baseName='Loader'
        contents {
            from processResources
            into ('lib') {
                from shadowJar
            }
        }
    }
}

processResources {
    //filter ReplaceTokens, tokens: ["LOADER_HOME": loaderpath ]
    doLast
    {
        def path = "$buildDir/resources/main/bin"
        //println "Link in loader util bash scripts... in $path"

        ant.symlink( resource: "./templateLoader.sh", link: "$path/DbMigrate")
        ant.symlink( resource: "./templateLoader.sh", link: "$path/LoaderCli")
        ant.symlink( resource: "./templateLoader.sh", link: "$path/RunReport")
        ant.symlink( resource: "./templateLoader.sh", link: "$path/VcfAnomaly")
        ant.symlink( resource: "./templateLoader.sh", link: "$path/VcfDbVerify")
        ant.symlink( resource: "./templateLoader.sh", link: "$path/PathosExporter")

        //  Make executable
        //
        ant.chmod( perm: '+x' ) { fileset(dir: path) { include(name: '*') } }
    }
}

//  Finds groovy here as well as dependent Jars
//
dependencies
{
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.6.1'
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.6.1'
    compile group: 'org.jdesktop', name: 'appframework', version: '1.0.3'
    compile group: 'cglib', name: 'cglib-nodep', version: '2.1_3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.2.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'commons-pool', name: 'commons-pool', version: '1.2'
    compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
    compile group: 'org.grails', name: 'grails-bootstrap', version: '1.1.2'
    compile group: 'org.grails', name: 'grails-core', version: '1.1.2'
    compile group: 'org.grails', name: 'grails-gorm', version: '1.1.2'
    compile group: 'org.grails', name: 'grails-spring', version: '1.1.2'
    compile group: 'org.grails', name: 'grails-web', version: '1.1.2'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.9'
    compile group: 'org.hibernate', name: 'hibernate-annotations', version: '3.3.0.ga'
    compile group: 'org.hibernate', name: 'hibernate-commons-annotations', version: '3.3.0.ga'
    compile group: 'org.hibernate', name: 'hibernate', version: '3.3.1.GA'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.2.8'
    compile group: 'javassist', name: 'javassist', version: '3.4.GA'
    compile group: 'javax.transaction', name: 'jta', version: '1.1'
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile group: 'org.hibernate', name: 'ejb3-persistence', version: '3.3.2.Beta1'
    compile group: 'org.springframework', name: 'spring', version: '2.5.6'
    compile group: 'org.springframework.security', name: 'spring-security-core', version: '3.2.1.RELEASE'
    compile group: 'org.springframework.security', name: 'spring-security-web', version: '3.2.1.RELEASE'
    compile group: 'org.hsqldb', name: 'sqltool', version: '2.2.8'
    compile group: 'org.swinglabs', name: 'swing-worker', version: '1.1'

    //  Peter Mac dependencies
    //
    compile group: 'org.petermac.pathos', name: 'yaml-tools', version: version
    compile group: 'org.petermac.pathos', name: 'pathos-api', version: version
    compile group: 'org.petermac.pathos', name: 'pathos-amqp', version: version
    compile( group: 'org.petermac.pathos' , name: 'PathosCore',  version: version, classifier: 'all' )
}

